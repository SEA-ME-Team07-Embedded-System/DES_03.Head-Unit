SUMMARY = "Calculator Utility from the Lumina Desktop"
HOMEPAGE = "https://lumina-desktop.org/"
LICENSE = "BSD-3-Clause"
LIC_FILES_CHKSUM = "file://${WORKDIR}/git/LICENSE;md5=5c6121c97944d0eef19da2dbc0925ba5"

REQUIRED_DISTRO_FEATURES = "x11"

QT_TRANSLATION_FILES = "${datadir}/*/i18n/*.qm"

inherit qmake5 features_check gtk-icon-cache qt5-translation manpages

DEPENDS += " \
    qttools-native \
    qtbase \
    qtsvg \
"

SRC_URI = "git://github.com/lumina-desktop/lumina-calculator.git;branch=master;protocol=https"
SRCREV = "31807e06a3a6bd194bad6b0fd204ca2d81cc3810"
S = "${WORKDIR}/git/src-qt5"
PV = "1.6.0"

do_configure:prepend() {
    # change paths by sed instead of endles escapes in 'DEFINES+=..' below
    sed -i 's:L_ETCDIR:QString("${sysconfdir}"):' `find ${S} -name *.cpp`
    sed -i 's:L_SHAREDIR:QString("${datadir}"):' `find ${S} -name *.cpp`
    sed -i 's:$${MAN_ZIP}:gzip -c:' `find ${S} -name *.pro`
}

# Override magic in src-qt5/OS-detect.pri to our paths
EXTRA_QMAKEVARS_PRE += ' \
    OS=Linux \
    PREFIX=${prefix} \
    L_BINDIR=${bindir} \
    L_LIBDIR=${libdir} \
    L_ETCDIR=${sysconfdir} \
    L_SHAREDIR=${datadir} \
    L_INCLUDEDIR=${STAGING_INCDIR} \
    L_SESSDIR=${datadir}/xsessions \
    L_MANDIR=${mandir} \
    LRELEASE=${OE_QMAKE_PATH_EXTERNAL_HOST_BINS}/lrelease \
    CONFIG+=WITH_I18N \
'
